import Math;

class EnemyJellyFish : Sprite {
	
	field lifetime;
	field center_x = x;
	field center_y = y;
	field squirt_range = 16;
	
	constructor(x, y) : base(x, y) {
		this.lifetime = floor((7 * floor(x / 16.0) + floor(y / 13.0)) % 60);
		this.center_x = x;
		this.center_y = y;
		this.width = 16;
		this.height = 16;
		this.left_facing = true;
		this.immune_to_gravity = true;
		this.image = '0';
	}
	
	function draw(is_moving, counter, camera_offset) {
		
		img = images.Get('sprites/Enemies/Jellyfish/' + this.image + '.png');
		xy = this.get_top_left()
		img_offset = images.GetOffset(img);
		x = xy[0] - camera_offset[0] + img_offset[0];
		y = xy[1] - camera_offset[1] + img_offset[1];
		
		img.blit(x, y);
	}
	
	function update(playScene) {
		this.lifetime++;
		x_offset = Math.sin(this.lifetime * 2 * 3.14159265 / 300) * 16;
		this.x = floor(this.center_x + x_offset);
		
		// drift down
		if (this.lifetime % 60 < 40) {
			progress = (this.lifetime % 60.0) / 40.0;
			if (progress > 0.8) {
				this.image = '1';
			} else {
				this.image = '0';
			}
		} else { // squirt up
			this.image = '2';
			progress = ((this.lifetime % 60.0) - 40) / 20.0;
			progress = 1 - progress;
		}
		distance = (progress * 2 - 1.0);
		this.y = floor(this.center_y + distance * this.squirt_range);
	}
}
