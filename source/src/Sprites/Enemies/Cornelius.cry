
class EnemyCornelius : Sprite {
	
	field next_jump = 30;
	
	static field JUMP_TIMEOUTS = [30, 60, 50];
	
	constructor(x, y) : base(x, y) {
		this.hp = 25;
		this.width = 19;
		this.height = 25;
		this.left_facing = true;
	}
		
	function draw(surface, is_moving, counter, camera_offset) {
		
		direction = this.left_facing ? 'left' : 'right';
		
		num = this.lifetime / 3 % 3;
		
		file = direction + num + '.png';
		
		img = images.Get('sprites/EvilWizardDude/' + file);
		
		xy = this.get_top_left();
		x = xy[0] - camera_offset[0];
		y = xy[1] - camera_offset[1];
		
		img.blit(x, y);
	}
	
	function GetPowerUp(counter, playScene) {
		// terrible hackery
		playScene.next = new CutSceneScene('endgame', new CreditsScene());
	}
	
	function update(playScene) {
		this.lifetime++;
		
		this.left_facing = true;
		
		if (this.lifetime == this.next_jump) {
			this.next_jump += JUMP_TIMEOUTS.choice();
			this.platform = null;
			this.on_ground = false;
			this.vy = -10;
		}
		
		if (this.lifetime % 20 == 19) {
			
			dy = this.y - playScene.player.y;
			dx = abs(this.x - playScene.player.x) + 0.0;
			
			vx = -3.0;
			
			if (dx == 0) dx = .1;
			
			vy = vx * dy / dx;
			
			playScene.enemies.add(new WizardShoot(this.x, this.y, floor(vx), floor(vy)));
		}
	}
}
